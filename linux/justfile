# Export default package version (can be overridden by setting GNOSISVPN_PACKAGE_VERSION)
export GNOSISVPN_PACKAGE_VERSION := env_var_or_default('GNOSISVPN_PACKAGE_VERSION', `date +%Y.%m.%d+build.%H%M%S`)

# Download binaries from GCP
download distribution arch:
    ./scripts/download-binaries.sh --distribution {{ distribution }} --architecture {{ arch }}

# Generate Debian changelog
changelog:
    ./scripts/generate-changelog.sh

# Generate manual pages for binaries
manual:
    ./scripts/generate-manual.sh

# Build nfpm package for GitHub releases (assumes binaries, changelog, and manual already exist)
package-nfpm distribution arch:
    ./scripts/build-nfpm-package.sh --distribution {{ distribution }} --architecture {{ arch }}

# Sign package (creates .asc and .sha256 files)
sign distribution arch:
    #!/usr/bin/env bash
    set -o errexit -o nounset -o pipefail
    PKG_FILE=$(ls build/packages/gnosisvpn* | grep "{{ distribution }}" | grep "{{ arch }}" | head -1)
    if [ -z "$PKG_FILE" ]; then
        echo "Error: Package file not found for {{ distribution }} {{ arch }}"
        exit 1
    fi
    echo "Signing $PKG_FILE..."
    # Create detached signature
    echo "$GNOSISVPN_GPG_PRIVATE_KEY_PASSWORD" | gpg --batch --pinentry-mode loopback --passphrase-fd 0 --armor --output "${PKG_FILE}.asc" --detach-sign "$PKG_FILE"
    # Create SHA256 checksum
    shasum -a 256 "$PKG_FILE" > "${PKG_FILE}.sha256"
    echo "Created ${PKG_FILE}.asc and ${PKG_FILE}.sha256"

# Build source package for distribution repositories (Debian/Ubuntu, etc.)
package distribution arch:
    ./scripts/generate-package.sh --package-version "${GNOSISVPN_PACKAGE_VERSION}" --distribution {{ distribution }} --architecture {{ arch }}

test-package packager arch:
    #!/usr/bin/env bash
    set -o errexit -o nounset -o pipefail
    if [ "{{packager}}" == "archlinux" ] && [ "{{arch}}" == "aarch64-linux" ]; then
        echo "Skipping test for archlinux aarch64-linux as it is not supported yet in GCP images."
        exit 0
    fi
    # trap './test-vm.sh delete {{packager}} {{arch}} 2>&1 | tee test-package-{{packager}}-{{arch}}.log' EXIT
    #./test-vm.sh create {{packager}} {{arch}} 2>&1 | tee test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh copy {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh install {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log

# Test package with desktop environment (XFCE + xrdp) for GUI testing
test-package-desktop packager arch:
    #!/usr/bin/env bash
    set -o errexit -o nounset -o pipefail
    if [ "{{packager}}" == "archlinux" ] && [ "{{arch}}" == "aarch64-linux" ]; then
        echo "Skipping test for archlinux aarch64-linux as it is not supported yet in GCP images."
        exit 0
    fi
    echo "Creating VM with desktop environment for GUI testing..."
    echo "NOTE: This target does NOT auto-delete the VM. Delete manually when done."
    echo ""
    ./test-vm.sh create {{packager}} {{arch}} 2>&1 | tee test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh copy {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh install {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh install-desktop {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    echo ""
    echo "=========================================="
    echo "VM is ready for desktop testing!"
    echo "=========================================="
    echo "To connect via RDP, run:"
    echo "  just rdp-connect {{packager}} {{arch}}"
    echo ""
    echo "To delete the VM when done:"
    echo "  just delete-test-vm {{packager}} {{arch}}"
    echo ""

# Connect to test VM via RDP
rdp-connect packager arch:
    ./test-vm.sh rdp {{packager}} {{arch}}

delete-test-vm packager arch:
    ./test-vm.sh delete {{packager}} {{arch}}

