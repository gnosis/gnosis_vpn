# Export default package version (can be overridden by setting GNOSISVPN_PACKAGE_VERSION)
export GNOSISVPN_PACKAGE_VERSION := env_var_or_default('GNOSISVPN_PACKAGE_VERSION', `date +%Y.%m.%d+build.%H%M%S`)

# Download binaries from GCP
download distribution arch:
    ./scripts/download-binaries.sh --distribution {{ distribution }} --architecture {{ arch }}

# Generate changelog
changelog:
    ./scripts/generate-changelog.sh

# Generate manual pages for binaries
manual:
    ./scripts/generate-manual.sh

# Build package for GitHub releases (assumes binaries, changelog, and manual already exist)
package distribution arch sign="false":
    #!/usr/bin/env bash
    set -o errexit -o nounset -o pipefail
    SIGN_FLAG=$([ "{{ sign }}" = "true" ] && echo "--sign" || echo "")
    ./scripts/generate-package.sh \
        --package-version "${GNOSISVPN_PACKAGE_VERSION}" \
        --distribution {{ distribution }} \
        --architecture {{ arch }} \
        $SIGN_FLAG

test-package packager arch:
    #!/usr/bin/env bash
    set -o errexit -o nounset -o pipefail
    if [ "{{packager}}" == "archlinux" ] && [ "{{arch}}" == "aarch64-linux" ]; then
        echo "Skipping test for archlinux aarch64-linux as it is not supported yet in GCP images."
        exit 0
    fi
    # trap './test-vm.sh delete {{packager}} {{arch}} 2>&1 | tee test-package-{{packager}}-{{arch}}.log' EXIT
    #./test-vm.sh create {{packager}} {{arch}} 2>&1 | tee test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh copy {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh install {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log

# Test package with desktop environment (XFCE + xrdp) for GUI testing
test-package-desktop packager arch:
    #!/usr/bin/env bash
    set -o errexit -o nounset -o pipefail
    if [ "{{packager}}" == "archlinux" ] && [ "{{arch}}" == "aarch64-linux" ]; then
        echo "Skipping test for archlinux aarch64-linux as it is not supported yet in GCP images."
        exit 0
    fi
    echo "Creating VM with desktop environment for GUI testing..."
    echo "NOTE: This target does NOT auto-delete the VM. Delete manually when done."
    echo ""
    ./test-vm.sh create {{packager}} {{arch}} 2>&1 | tee test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh copy {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh install {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    ./test-vm.sh install-desktop {{packager}} {{arch}} 2>&1 | tee -a test-package-{{packager}}-{{arch}}.log
    echo ""
    echo "=========================================="
    echo "VM is ready for desktop testing!"
    echo "=========================================="
    echo "To connect via RDP, run:"
    echo "  just rdp-connect {{packager}} {{arch}}"
    echo ""
    echo "To delete the VM when done:"
    echo "  just delete-test-vm {{packager}} {{arch}}"
    echo ""

# Connect to test VM via RDP
rdp-connect packager arch:
    ./test-vm.sh rdp {{packager}} {{arch}}

delete-test-vm packager arch:
    ./test-vm.sh delete {{packager}} {{arch}}

